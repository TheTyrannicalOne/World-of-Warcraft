# Marksmanship Hunter
# November 23, 2020

actions.precombat+=/tar_trap,if=runeforge.soulforge_embers
actions.precombat+=/double_tap,precast_time=10,if=!covenant.kyrian&(!talent.volley|active_enemies<2)
actions.precombat+=/use_item,name=azsharas_font_of_power
actions.precombat+=/worldvein_resonance
actions.precombat+=/guardian_of_azeroth
actions.precombat+=/memory_of_lucid_dreams
actions.precombat+=/aimed_shot,if=active_enemies<3
actions.precombat+=/steady_shot,if=active_enemies>2

actions+=/counter_shot
# TODO:  This will need support to allow Counter Shot when target is not casting.
actions+=/counter_shot,line_cd=30,if=runeforge.sephuzs_proclamation|soulbind.niyas_tools_poison|(conduit.reversal_of_fortune&!runeforge.sephuzs_proclamation)
actions+=/use_items
actions+=/use_item,name=lurkers_insidious_gift,if=cooldown.trueshot.remains_guess<15|fight_remains<30
actions+=/use_item,name=azsharas_font_of_power,if=(fight_remains>cooldown+34|target.health.pct<20|target.time_to_pct_20<15)&cooldown.trueshot.remains_guess<15|fight_remains<35
actions+=/use_item,name=lustrous_golden_plumage,if=cooldown.trueshot.remains_guess<5|fight_remains<20
actions+=/use_item,name=galecallers_boon,if=buff.trueshot.remains>14|!talent.calling_the_shots.enabled|fight_remains<10
actions+=/use_item,name=ashvanes_razor_coral,if=buff.trueshot.remains>14&(buff.guardian_of_azeroth.up|!essence.condensed_lifeforce.major&ca_active)|debuff.razor_coral_debuff.down|fight_remains<20
actions+=/use_item,name=pocketsized_computation_device,if=!buff.trueshot.up&!essence.blood_of_the_enemy.major|debuff.blood_of_the_enemy.up|fight_remains<5
# Try to line up activated trinkets with Trueshot
actions+=/use_items
actions+=/call_action_list,name=cds
actions+=/call_action_list,name=st,if=active_enemies<3
actions+=/call_action_list,name=trickshots,if=active_enemies>2

actions.cds=berserking,if=buff.trueshot.up|fight_remains<13
actions.cds+=/blood_fury,if=buff.trueshot.up|fight_remains<16
actions.cds+=/ancestral_call,if=buff.trueshot.up|fight_remains<16
actions.cds+=/fireblood,if=buff.trueshot.up|fight_remains<9
actions.cds+=/lights_judgment,if=buff.trueshot.down
actions.cds+=/bag_of_tricks,if=buff.trueshot.down
actions.cds+=/potion,if=buff.trueshot.up&buff.bloodlust.up|buff.trueshot.up&target.health.pct<20|fight_remains<26
actions.cds+=/reaping_flames,if=buff.trueshot.down&(target.health.pct>80|target.health.pct<=20|target.time_to_pct_20>30)
actions.cds+=/worldvein_resonance,if=(trinket.azsharas_font_of_power.cooldown.remains>20|!equipped.azsharas_font_of_power|fight_remains<trinket.azsharas_font_of_power.cooldown.duration+34&target.health.pct>20)&(cooldown.trueshot.remains_guess<3|(essence.vision_of_perfection.minor&fight_remains>cooldown+buff.worldvein_resonance.duration))|fight_remains<20
actions.cds+=/guardian_of_azeroth,if=(ca_active|fight_remains>cooldown+30)&(buff.trueshot.up|cooldown.trueshot.remains<16)|fight_remains<31
actions.cds+=/ripple_in_space,if=cooldown.trueshot.remains<7
actions.cds+=/memory_of_lucid_dreams,if=!buff.trueshot.up

actions.st=steady_shot,if=talent.steady_focus&(prev_gcd.1.steady_shot&buff.steady_focus.remains<5|buff.steady_focus.down)
actions.st+=/kill_shot
actions.st+=/double_tap,if=covenant.kyrian&cooldown.resonating_arrow.remains<gcd|!covenant.kyrian&(cooldown.aimed_shot.up|cooldown.rapid_fire.remains>cooldown.aimed_shot.remains)
actions.st+=/flare,if=tar_trap.up&runeforge.soulforge_embers
actions.st+=/tar_trap,if=runeforge.soulforge_embers&tar_trap.remains<gcd&cooldown.flare.remains<gcd
actions.st+=/explosive_shot
actions.st+=/wild_spirits
actions.st+=/flayed_shot
actions.st+=/death_chakram,if=focus+cast_regen<focus.max
actions.st+=/volley,if=buff.precise_shots.down|!talent.chimaera_shot|active_enemies<2
actions.st+=/a_murder_of_crows
actions.st+=/resonating_arrow
actions.st+=/trueshot,if=buff.precise_shots.down|buff.resonating_arrow.up|buff.wild_spirits.up|buff.volley.up&active_enemies>1
# TODO: Review action.serpent_sting.in_flight_to_target
actions.st+=/aimed_shot,cycle_targets=1,if=buff.precise_shots.down|(buff.trueshot.up|full_recharge_time<gcd+cast_time)&(!talent.chimaera_shot|active_enemies<2)|buff.trick_shots.remains>execute_time&active_enemies>1
actions.st+=/rapid_fire,if=focus+cast_regen<focus.max&(buff.trueshot.down|!runeforge.eagletalons_true_focus)&(buff.double_tap.down|talent.streamline)
actions.st+=/chimaera_shot,if=buff.precise_shots.up|focus>cost+action.aimed_shot.cost
actions.st+=/arcane_shot,if=buff.precise_shots.up|focus>cost+action.aimed_shot.cost
actions.st+=/serpent_sting,cycle_targets=1,if=refreshable&target.time_to_die>duration
actions.st+=/barrage,if=active_enemies>1
actions.st+=/rapid_fire,if=focus+cast_regen<focus.max&(buff.double_tap.down|talent.streamline)
actions.st+=/blood_of_the_enemy,if=buff.trueshot.up&(buff.unerring_vision.stack>4|!azerite.unerring_vision.enabled)|fight_remains<11
actions.st+=/focused_azerite_beam,if=!buff.trueshot.up|fight_remains<5
actions.st+=/purifying_blast,if=!buff.trueshot.up|fight_remains<8
actions.st+=/concentrated_flame,if=focus+focus.regen*gcd<focus.max&buff.trueshot.down&(!dot.concentrated_flame_burn.remains&!action.concentrated_flame.in_flight)|full_recharge_time<gcd|fight_remains<5
actions.st+=/the_unbound_force,if=buff.reckless_force.up|buff.reckless_force_counter.stack<10|fight_remains<5
actions.st+=/steady_shot

actions.trickshots=steady_shot,if=talent.steady_focus&in_flight&buff.steady_focus.remains<5
actions.trickshots+=/double_tap,if=covenant.kyrian&cooldown.resonating_arrow.remains<gcd|cooldown.rapid_fire.remains<cooldown.aimed_shot.full_recharge_time|!(talent.streamline&runeforge.surging_shots)|!covenant.kyrian
actions.trickshots+=/tar_trap,if=runeforge.soulforge_embers&tar_trap.remains<gcd&cooldown.flare.remains<gcd
actions.trickshots+=/flare,if=tar_trap.up&runeforge.soulforge_embers
actions.trickshots+=/explosive_shot
actions.trickshots+=/wild_spirits
actions.trickshots+=/resonating_arrow
actions.trickshots+=/volley
actions.trickshots+=/barrage
actions.trickshots+=/trueshot
actions.trickshots+=/rapid_fire,if=buff.trick_shots.remains>=execute_time&runeforge.surging_shots&buff.double_tap.down
actions.trickshots+=/aimed_shot,cycle_targets=1,if=buff.trick_shots.remains>=execute_time&(buff.precise_shots.down|full_recharge_time<cast_time+gcd|buff.trueshot.up)
actions.trickshots+=/death_chakram,if=focus+cast_regen<focus.max
actions.trickshots+=/rapid_fire,if=buff.trick_shots.remains>=execute_time
actions.trickshots+=/multishot,if=buff.trick_shots.down|buff.precise_shots.up&focus>cost+action.aimed_shot.cost&(!talent.chimaera_shot|active_enemies>3)
actions.trickshots+=/chimaera_shot,if=buff.precise_shots.up&focus>cost+action.aimed_shot.cost&active_enemies<4
actions.trickshots+=/kill_shot,if=buff.dead_eye.down
actions.trickshots+=/focused_azerite_beam
actions.trickshots+=/purifying_blast
actions.trickshots+=/concentrated_flame
actions.trickshots+=/blood_of_the_enemy,if=prev_gcd.1.volley|!talent.volley.enabled|fight_remains<11
actions.trickshots+=/the_unbound_force,if=buff.reckless_force.up|buff.reckless_force_counter.stack<10
actions.trickshots+=/a_murder_of_crows
actions.trickshots+=/flayed_shot
actions.trickshots+=/serpent_sting,cycle_targets=1,if=refreshable
actions.trickshots+=/multishot,if=focus>cost+action.aimed_shot.cost
actions.trickshots+=/steady_shot