# Arms Warrior
# November 24, 2020

# Changes:
# - Added Pummel.
# - Added Victory Rush/Impending Victory.
# - Added Battle Shout to precombat APL.
# - Executioner's Precision is deleted.
# - Added Use Items.
# - Tweak entry for Bladestorm.

# Executed before combat begins. Accepts non-harmful actions only.
actions.precombat+=/battle_shout
actions.precombat+=/use_item,name=azsharas_font_of_power
actions.precombat+=/memory_of_lucid_dreams,if=talent.fervor_of_battle.enabled|!talent.fervor_of_battle.enabled&time_to_die>150
actions.precombat+=/guardian_of_azeroth,if=talent.fervor_of_battle.enabled|talent.massacre.enabled&time_to_die>210|talent.rend.enabled&(time_to_die>210|time_to_die<145)

# Executed every time the actor is available.
actions=charge
actions+=/pummel
actions+=/potion
actions+=/blood_fury,if=debuff.colossus_smash.up
actions+=/berserking,if=debuff.colossus_smash.remains>6
actions+=/arcane_torrent,if=cooldown.mortal_strike.remains>1.5&rage<50
actions+=/lights_judgment,if=debuff.colossus_smash.down&cooldown.mortal_strike.remains
actions+=/fireblood,if=debuff.colossus_smash.up
actions+=/ancestral_call,if=debuff.colossus_smash.up
actions+=/bag_of_tricks,if=debuff.colossus_smash.down&cooldown.mortal_strike.remains
actions+=/use_items
actions+=/sweeping_strikes,if=spell_targets.whirlwind>1&cooldown.bladestorm.remains>12
actions+=/blood_of_the_enemy,if=buff.test_of_might.up|(debuff.colossus_smash.up&!azerite.test_of_might.enabled)
actions+=/purifying_blast,if=!debuff.colossus_smash.up&!buff.test_of_might.up
actions+=/ripple_in_space,if=!debuff.colossus_smash.up&!buff.test_of_might.up
actions+=/worldvein_resonance,if=!debuff.colossus_smash.up&!buff.test_of_might.up
actions+=/focused_azerite_beam,if=!debuff.colossus_smash.up&!buff.test_of_might.up
actions+=/reaping_flames,if=!debuff.colossus_smash.up&!buff.test_of_might.up
actions+=/concentrated_flame,if=!debuff.colossus_smash.up&!buff.test_of_might.up&dot.concentrated_flame_burn.remains=0
actions+=/the_unbound_force,if=buff.reckless_force.up
actions+=/guardian_of_azeroth,if=cooldown.colossus_smash.remains<10
actions+=/memory_of_lucid_dreams,if=!talent.warbreaker.enabled&cooldown.colossus_smash.remains<gcd&(target.time_to_die>150|target.health.pct<20)
actions+=/memory_of_lucid_dreams,if=talent.warbreaker.enabled&cooldown.warbreaker.remains<gcd&(target.time_to_die>150|target.health.pct<20)
actions+=/run_action_list,name=hac,if=raid_event.adds.exists
actions+=/run_action_list,name=execute,if=(talent.massacre.enabled&target.health.pct<35)|target.health.pct<20|(target.health.pct>80&covenant.venthyr)
actions+=/run_action_list,name=single_target

actions.execute=deadly_calm
actions.execute+=/rend,if=remains<=duration*0.3
actions.execute+=/skullsplitter,if=rage<60&(!talent.deadly_calm.enabled|buff.deadly_calm.down)
actions.execute+=/avatar,if=cooldown.colossus_smash.remains<1
actions.execute+=/conquerors_banner
actions.execute+=/cleave,if=spell_targets.whirlwind>1&dot.deep_wounds.remains<gcd
actions.execute+=/warbreaker
actions.execute+=/colossus_smash
actions.execute+=/overpower,if=charges=2
actions.execute+=/ancient_aftershock
actions.execute+=/spear_of_bastion
actions.execute+=/mortal_strike,if=dot.deep_wounds.remains<gcd
actions.execute+=/skullsplitter,if=rage<40
actions.execute+=/overpower
actions.execute+=/condemn
actions.execute+=/execute
actions.execute+=/bladestorm,if=rage<80
actions.execute+=/ravager,if=rage<80
actions.execute+=/victory_rush
actions.execute+=/impending_victory

actions.hac=skullsplitter,if=rage<60&buff.deadly_calm.down
actions.hac+=/conquerors_banner
actions.hac+=/avatar,if=cooldown.colossus_smash.remains<1
actions.hac+=/cleave,if=dot.deep_wounds.remains<=gcd
actions.hac+=/warbreaker
actions.hac+=/ancient_aftershock
actions.hac+=/spear_of_bastion
actions.hac+=/bladestorm
actions.hac+=/ravager
actions.hac+=/colossus_smash
actions.hac+=/rend,if=remains<=duration*0.3&buff.sweeping_strikes.up
actions.hac+=/cleave
actions.hac+=/mortal_strike,if=buff.sweeping_strikes.up|dot.deep_wounds.remains<gcd&!talent.cleave.enabled
actions.hac+=/overpower,if=talent.dreadnaught.enabled
actions.hac+=/condemn
actions.hac+=/execute,if=buff.sweeping_strikes.up
actions.hac+=/overpower
actions.hac+=/whirlwind

actions.single_target=conquerors_banner,if=(target.time_to_die>180|(target.health.pct<20|talent.massacre.enabled&target.health.pct<35))
actions.single_target+=/avatar,if=cooldown.colossus_smash.remains<1
actions.single_target+=/rend,if=remains<=duration*0.3
actions.single_target+=/cleave,if=spell_targets.whirlwind>1&dot.deep_wounds.remains<gcd
actions.single_target+=/warbreaker
actions.single_target+=/colossus_smash
actions.single_target+=/ancient_aftershock
actions.single_target+=/spear_of_bastion
actions.single_target+=/bladestorm,if=debuff.colossus_smash.up&!covenant.venthyr
actions.single_target+=/ravager,if=debuff.colossus_smash.up&!covenant.venthyr
actions.single_target+=/overpower,if=charges=2
actions.single_target+=/mortal_strike,if=buff.overpower.stack>=2&buff.deadly_calm.down|dot.deep_wounds.remains<=gcd
actions.single_target+=/deadly_calm
actions.single_target+=/skullsplitter,if=rage<60&buff.deadly_calm.down
actions.single_target+=/overpower
actions.single_target+=/condemn
actions.single_target+=/execute,if=buff.sudden_death.react
actions.single_target+=/mortal_strike
actions.single_target+=/bladestorm,if=debuff.colossus_smash.up&covenant.venthyr
actions.single_target+=/whirlwind,if=talent.fervor_of_battle.enabled&rage>60
actions.single_target+=/slam,if=rage>50
actions.single_target+=/victory_rush
actions.single_target+=/impending_victory